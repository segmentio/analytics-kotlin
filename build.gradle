// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.8.0"
    ext.writeKey = "hAh3QxoRCsQsnyabTLMpWbpxoIN4O2mU"
    repositories {
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"

        // needed for android unit tests
        classpath("de.mannodermaus.gradle.plugins:android-junit5:1.9.3.0")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:4.3.15'
    }
}

plugins {
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = ['-Xjvm-default=all|all-compatibility'] //enable or compatibility
            jvmTarget = "1.8"
        }
    }
    group GROUP
    version getVersionName()
}

snyk {
    // severity = 'high'
    autoDownload = true
    autoUpdate = true
    arguments = '--all-sub-projects --fail-on=upgradable'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getVersionName() { // If not release build add SNAPSHOT suffix
    return hasProperty('release') ? VERSION_NAME : VERSION_NAME+"-SNAPSHOT"
}

apply from: rootProject.file('gradle/promote.gradle')
apply from: rootProject.file('gradle/codecov.gradle')