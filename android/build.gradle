plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true

        minSdkVersion 16
        targetSdkVersion 30
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("int", "SEGMENT_VERSION_CODE", "${versionCode}")
        buildConfigField("String", "SEGMENT_VERSION_NAME", "\"${versionName}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // MAIN DEPS
    api project(':core')
    api 'com.github.segmentio:sovran-kotlin:1.2.0'
    api "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'androidx.lifecycle:lifecycle-process:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'

    // STD LIBS
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // TESTING
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'io.mockk:mockk:1.10.6'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Add JUnit5 dependencies.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'

    // Add JUnit4 legacy dependencies.
    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.7.2'

    // Add Roboelectric dependencies.
    testImplementation 'org.robolectric:robolectric:4.5'
    testImplementation 'androidx.test:core:1.4.0'
}

apply from: rootProject.file('gradle/jitpack-android.gradle')
apply from: rootProject.file('gradle/mvn-publish.gradle')
apply from: rootProject.file('gradle/codecov.gradle')